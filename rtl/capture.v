// capture.v
//
// This module captures incoming pixel data and is synchronous to the
// 24 MHz clock generated by the XCLK pin of the OV7670 camera.
//
// The OV7670 outputs 30FPS in VGA.
//
module capture 
	(
	input  wire        i_pclk,     // 24 MHz; sourced from OV7670 camera
	input  wire        i_rstn,     // synchronous active low reset
	input  wire        i_cfg_done, // config done flag from 75MHz domain

	// OV7670 camera interface
	input  wire        i_vsync,    // active-high, indicates start of frame
	input  wire        i_href,     // active-high, indicates row data transmission
	input  wire [7:0]  i_data,     // pixel data from camera
    
	// FIFO write interface   
	output reg         o_wr,       // fifo write enable
	output reg  [11:0] o_wdata,    // fifo write data; {red, green, blue}
	input  wire        i_full      // fifo full flag
	);

	reg        nxt_wr;
	reg [11:0] nxt_wdata;
	reg [7:0]  byte1_data, nxt_byte1_data;

	reg        pixel_half, nxt_pixel_half;

	reg [1:0]  STATE, NEXT_STATE;
	localparam STATE_IDLE    = 1,
	           STATE_ACTIVE  = 2,
	           STATE_INITIAL = 0;

	initial STATE = STATE_INITIAL;

// **** Next State Logic ****
//
	always@* begin
		nxt_wr           = 0;
		nxt_wdata        = o_wdata;
		nxt_byte1_data   = byte1_data;
		nxt_pixel_half   = pixel_half;
		NEXT_STATE       = STATE;

		case(STATE)

			// don't capture until configuration is done
			STATE_INITIAL: begin
				NEXT_STATE = (i_cfg_done) ? STATE_IDLE : STATE_INITIAL;
			end

			// idle state; camera is not outputting
			STATE_IDLE: begin
				nxt_wr         = 0;
				nxt_pixel_half = 0;
				// nxt_framecounter = 0;
				NEXT_STATE = (!i_vsync) ? STATE_ACTIVE : STATE_IDLE;
			end

			// active state; camera outputting display data
			// -> continuously write to fifo as long as href is high
			//
			STATE_ACTIVE: begin
				if(i_href) begin
					nxt_pixel_half = ~nxt_pixel_half;

					// RGB444: Second Byte (green, blue)
					if(pixel_half) begin
						nxt_wr         = 1;
						nxt_wdata      = {byte1_data[3:0], i_data};
					end

					// RGB444: First Byte (red)
					else begin
						nxt_wr              = 0;
						nxt_byte1_data[3:0] = i_data[3:0];
					end          
				end
				
				NEXT_STATE = (i_vsync) ? STATE_IDLE : STATE_ACTIVE;
			end
		endcase
	end

//
//
//
	always@(posedge i_pclk) begin
		if(!i_rstn) begin
			o_wr         <= 0;
			o_wdata      <= 0;
			byte1_data   <= 0;
			pixel_half   <= 0;
			STATE        <= STATE_INITIAL;
		end
		else begin
			o_wr         <= nxt_wr;
			o_wdata      <= nxt_wdata;
			byte1_data   <= nxt_byte1_data;
			pixel_half   <= nxt_pixel_half;
			STATE        <= NEXT_STATE;
		end
	end

endmodule
